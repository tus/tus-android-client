plugins {
    id('com.android.library')
    id('signing')
}

android {
    compileSdk 35
    setBuildToolsVersion("35.0.0")

    defaultConfig {
        minSdkVersion 14
        targetSdk 35
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    namespace 'io.tus.android.client'
}

def config = new ConfigSlurper().parse(new File("${projectDir}/src/main/res/tus-android-client-version/version.properties").toURI().toURL())
description=config.descritpion
version=config.version

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'io.tus.java.client:tus-java-client:0.5.0'
    implementation 'androidx.annotation:annotation-jvm:1.9.1'
    implementation 'androidx.work:work-runtime:2.10.0'
    implementation 'joda-time:joda-time:2.9.1'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'org.robolectric:robolectric:4.14.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:3.0'
}

afterEvaluate {
    tasks.register('sourcesJar', Jar) {
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.srcDirs
    }

    tasks.register('javadoc', Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        android.libraryVariants.all { variant ->
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }
}

def pomConfig = {
    name 'tus-android-client'
    url 'http://tus.io'

    developers {
        developer {
            id 'acconut'
            name 'Marius Kleidl'
            email 'marius@transloadit.com'
        }
        developer {
            id 'cdr-chakotay'
            name 'Florian Kuenzig'
            email 'florian@transloadit.com'
        }
    }

    inceptionYear '2015'
    licenses {
        license {
            name 'The MIT License (MIT)'
            url 'http://opensource.org/licenses/MIT'
        }
    }

    scm {
        url 'https://github.com/tus/tus-android-client'
        connection 'https://github.com/tus/tus-android-client'
        developerConnection 'https://github.com/tus/tus-android-client'
    }
}

publishing {
    publications {
        androidRelease(MavenPublication) {
            afterEvaluate {
                from components.findByName('release')
                groupId 'io.tus.android.client'
                artifactId 'tus-android-client'
                description project.getDescription()
                version project.getVersion()

                artifact sourcesJar
                artifact javadocJar
                artifact (project.layout.buildDirectory.dir('outputs/aar/tus-android-client-release.aar')) {
                    builtBy assemble
                }

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.getDescription())
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

signing {
    def signingKeyId = System.getenv('SIGNING_KEY_ID')
    def signingPassword = System.getenv('SIGNING_KEY_PASSWORD')
    def signingKey = System.getenv('SIGNING_KEY_AMORED')
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications.androidRelease
}
